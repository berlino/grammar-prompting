query : list_value | value

list_value: "(listValue" value ")" 
    | "(listValue" list_value ")"
    | "(singleton" SINGLETON_VALUE ")"
    | "(domain" property ")"
    | "(concat" value value ")"
    | "(concat" list_value list_value ")"
    | "(ensureNumericEntity" numerical_value ")"
    | "(ensureNumericEntity" list_value ")"
    | "(filter" list_value property ")"
    | "(filter" list_value property OP ")"
    | "(filter" list_value property OP value ")"
    | "(filter" list_value property OP list_value ")"
    | "(superlative" list_value AGGREGATE property ")"
    | "(superlative" list_value AGGREGATE property list_value ")"
    | "(countSuperlative" list_value AGGREGATE property ")"
    | "(countSuperlative" list_value AGGREGATE property list_value ")"
    | "(countComparative" list_value property OP NUMBER_VALUE ")"
    | "(countComparative" list_value property OP NUMBER_VALUE list_value ")"
    | "(getProperty" list_value property ")"
    | "(getProperty" value property ")"
    | "(aggregate" AGGREGATE list_value ")"

property: "(reverse" property ")"
    | "(ensureNumericProperty" property ")"
    | PROPERTY

PROPERTY: "! type"
    | "player" 
    | "position" 
    | "team" 
    | "season"
    | "num_points"
    | "num_assists"
    | "num_steals"
    | "num_turnovers"
    | "num_rebounds"
    | "num_blocks"
    | "num_fouls"
    | "num_games_played"

value: ENTITY_VALUE | numerical_value | "(_size" list_value ")" 
numerical_value: NUMBER_VALUE | DATE_VALUE

SINGLETON_VALUE: "en.player" | "en.team" | "en.position"
ENTITY_VALUE: "en.player.kobe_bryant"
    | "en.player.lebron_james"
    | "en.team.lakers"
    | "en.team.cavaliers"
    | "en.position.point_guard"
    | "en.position.forward"
NUMBER_VALUE: "3 point"
    | "3 assist"
    | "3 steal"
    | "3 turnover"
    | "3 rebound"
    | "3 block"
    | "3 foul"
    | "3 game"
    | "2"
DATE_VALUE: "2004 -1 -1"
    | "2010 -1 -1"

OP: "=" | ">" | "<" | ">=" | "<=" | "! ="
AGGREGATE: "sum" | "max" | "min" | "avg"

%import common.WS
%ignore WS